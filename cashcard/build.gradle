plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4' apply false
	id 'io.spring.dependency-management' version '1.1.4'
}

java {
	sourceCompatibility = '17'
}

ext {
	set('springCloudVersion', "2023.0.0")
}

allprojects {
	group = 'com.example'
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'io.spring.dependency-management'

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-stream'
		implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
		implementation 'org.springframework.kafka:spring-kafka'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.cloud:spring-cloud-stream-test-binder'
		testImplementation 'org.springframework.kafka:spring-kafka-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	test {
		useJUnitPlatform()
	}
}

project('cashcard-transaction-e2e-test') {

	dependencies {
		implementation project(':cashcard-transaction-source')
		implementation project(':cashcard-transaction-enricher')
		implementation project(':cashcard-transaction-sink')
		testImplementation 'org.testcontainers:kafka'
		testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	}
}




